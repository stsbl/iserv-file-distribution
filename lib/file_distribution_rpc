#!/usr/bin/perl -T -CSDAL

use warnings;
use strict;
use utf8;
use IPC::Run;
use IServ::Conf;
use IServ::DB;
use IServ::Locale;
use IServ::RPC;
use Parallel::ForkManager;
use Stsbl::IServ::FileDistribution;
use Stsbl::IServ::IO;
use Stsbl::IServ::RPC;
use Stsbl::IServ::Security;
umask 0002;

POSIX::setlocale(POSIX::LC_CTYPE, $conf->{DefLang} . ".UTF-8");
POSIX::setlocale(POSIX::LC_MESSAGES, $conf->{DefLang} . ".UTF-8");
IServ::Locale::UseDefLang();

die "usage: file_distribution_netrpc account command ip [ip...]\n" if @ARGV < 3;

my $m_title = qr/^([ !#&()*+,\-.0-9:<=>?\@A-Z\[\]^_a-z{|}~\P{ascii}]{1,64})$/;
# forbidden: 00-1f " $ % ' / ; \ ` 7f

my ($act) = (shift @ARGV) =~ /^([\w.-]+)$/ or die "Account invalid!\n";
my ($cmd) = (shift @ARGV) =~ /^(\w+)$/ or die "Command invalid!\n";

my (@ips, @ips_online_win, %ips_online_lin);
for (@ARGV)
{
  # +IP = Windows ++IP = *nix
  my ($ip) = /^\+?\+?([\d.]+)$/ or die "ip invalid\n";
  push @ips, $ip;
  $ips_online_lin{$ip} = 1 if /^\+\+/;
}

if (Stsbl::IServ::Security::sudo_context)
{
  # run as unprivileged user
  my ($sesspw) = $ENV{SESSPW} =~ /^(\w{1,256})$/ or die "SESSPW invalid!\n";
  Stsbl::IServ::Security::verify_uid $act;

  Stsbl::IServ::Security::set_credentials $act, $sesspw;
  Stsbl::IServ::Security::sessauth_login "file_distribution_rpc";
} else 
{
  # no password required if running directly as root
  Stsbl::IServ::Security::set_credentials $act, undef;
}

my (undef, undef, $uid, $gid, undef, undef, undef, $home) = getpwnam $act
  or die "User not found: $act\n";

my $arg;
($arg) = grep { utf8::decode $_ } $ENV{ARG} =~ /(.*)/s if exists $ENV{ARG};

my $isolation;
if (defined $ENV{FD_ISOLATION}) 
{
  $isolation = $ENV{FD_ISOLATION} if $ENV{FD_ISOLATION} =~ /^0|1$/;
} else {
  $isolation = 0;
}

# convert to perl boolean
if (defined $isolation) {
  $isolation = 0 if $isolation eq '0';
  $isolation = 1 if $isolation eq '1';
}

undef %ENV;
$ENV{PATH} = "/usr/sbin:/usr/bin:/sbin:/bin";

my $pm = new Parallel::ForkManager(100);

sub req_ownership($)
{
  my ($ip) = @_;
  return if $act eq "root";
  return if not IServ::DB::Do "SELECT 1 FROM file_distribution WHERE ip = ?", $ip;
  my ($curAct) =  IServ::DB::SelectRow "SELECT Act FROM file_distribution WHERE ip = ?", $ip; 
  error "need ownership of file distribution for ip $ip" 
    unless $curAct eq $act;
} 

sub run_parallel(&@)
{
  my ($func, @ips) = @_;
  IPS:
  for (@ips) {
    $pm->start and next IPS;
    &$func();
    $pm->finish;
  }
  $pm->wait_all_children;
}

# determine online IPs
# Originally, the calling script provided this information based on the results
# of /usr/lib/iserv/multiping. Unfortunately, multiping is optimized for
# performance (to ensure that the computer management loads quickly). This
# meant that a computer that is online but was missed by multiping would
# accidentally be ignored here. We now use our own nmap call to prevent this.
IPC::Run::run ["nmap", "-n", "-sP", "-oG", "-", "--", @ips],
    ">", IPC::Run::new_chunker("\n"),
    sub
    {
      local $_ = shift;
      if (/^Host:\s+([\d\.]+)\s+\(\)\s+Status:\s+Up.*$/i)
      {
        push @ips_online_win, $1 if !$ips_online_lin{$1};
      }
    };

if ($cmd eq "fdon")
{
  req_priv_or_root "file_distribution";
  req_priv_or_root "computer_boot";
  rpc_linux_req_nologin(keys %ips_online_lin);

  die "File distribution title invalid!" unless defined $arg and $arg =~ $m_title;

  # prepare data folders
  for ("$home/File-Distribution", "$home/File-Distribution/$arg", 
    "$home/File-Distribution/$arg/Assignment", "$home/File-Distribution/$arg/Return")
  {
    -d or mkdir $_, 02770 or die "Cannot create directory $_: $!\n";
    chmod 02770, $_ or die "Cannot chmod directory $_: $!\n";
    chown $uid, $gid, $_ or die "Cannot chown directory $_: $!\n";
  }

  my $symlink = "$home/Files/File-Distribution";
  unlink $symlink if -l $symlink;
  symlink "../File-Distribution", $symlink or warn "Cannot create symlink $symlink: $!\n";
  
  foreach (@ips) { 
    my $exists = IServ::DB::Do "SELECT 1 FROM file_distribution WHERE ip = ?", $_;
    IServ::DB::Do "INSERT INTO file_distribution (Title, Act, IP, Isolation) VALUES (?, ?, ?, ?)", $arg, $act, $_, $isolation if not $exists;
    warn "Cannot enable file distribution for $_ twice!" if $exists;
  }
  system "/usr/lib/iserv/file_distribution_cfg";

  my $notice = _("Someone started a file distribution for your host. To access the files,".
    ' click on the symbol "Auf bereitgestellte Dateien zugreifen" on the desktop.');
  run_parallel { msg Stsbl::IServ::RPC::rpc_message_unicode $_, $notice } @ips_online_win;
  run_parallel { msg IServ::RPC::winexe $_, IServ::RPC::netlogon "file-distribution.bat", "start"; } @ips_online_win;
  run_parallel { linux_start; } keys %ips_online_lin;
} 
elsif ($cmd eq "fdoff") 
{
  req_priv_or_root "file_distribution";
  req_priv_or_root "computer_boot";
  req_ownership $_ for @ips;
  rpc_linux_req_nologin(keys %ips_online_lin);

  IServ::DB::Do "DELETE FROM file_distribution WHERE ip = ?", $_ for @ips;
  run_parallel { msg IServ::RPC::winexe $_, IServ::RPC::netlogon "file-distribution.bat", "stop"; } @ips_online_win;
  run_parallel { linux_stop; } keys %ips_online_lin;
  system "/usr/lib/iserv/file_distribution_cfg"; 
}
elsif ($cmd eq "soundoff")
{
  req_priv_or_root "computer_boot";
  req_priv_or_root "file_distribution";
  IServ::DB::Do "DELETE FROM computer_sound_lock WHERE IP = ?", $_ for @ips;
  IServ::DB::Do "INSERT INTO computer_sound_lock (IP, Act) VALUES (?, ?)", $_, $act for @ips;
  run_parallel { msg IServ::RPC::winexe $_, "cmd", "/c", "net", "stop", "audiosrv", ">NUL", "2>&1"; } @ips_online_win;
}
elsif ($cmd eq "soundon")
{
  req_priv_or_root "computer_boot";
  req_priv_or_root "file_distribution";
  IServ::DB::Do "DELETE FROM computer_sound_lock WHERE IP = ?", $_ for @ips;
  run_parallel { msg IServ::RPC::winexe $_, "cmd", "/c", "net", "start", "audiosrv", ">NUL", "2>&1"; } @ips_online_win;
}
else {
  die "Unknown command $cmd!";
}
